entity Enemy {
    var @x, @y, @y_velocity;

    constructor {
        @x = 20;
        @y = 0;

        @y_velocity = 0.25;
    }

    tick {
        this.tick_movement();
        this.tick_hit();
    }

    func tick_movement() {
        @y = @y + @y_velocity;

        if (@y >= Display.height() - 3) {
            @y_velocity = -0.25;
        }

        if (@y <= 0) {
            @y_velocity = 0.25;
        }
    }

    func tick_hit() {
        each bullet in (ShipBullet.all()) {
            if (bullet.has_hit(@x, @y, 3, 3)) {
                destroy this;
                destroy bullet;

                (sound { 0.1: A }).play();
            }
        }
    }

    draw {
        return sprite {
            ###
            .#.
            #.#
        };
    }
}
