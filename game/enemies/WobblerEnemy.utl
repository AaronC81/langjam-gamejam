entity WobblerEnemy {
    use Positional;
    use TickHit;

    var @x_velocity, @y_velocity;

    constructor {
        @x = Display.width() + 1;
        this.randomise_y(3);

        @x_velocity = -0.1;
        @y_velocity = 0.25;
    }

    tick {
        this.tick_movement();
    }

    func tick_movement() {
        @x = @x + @x_velocity;
        @y = @y + @y_velocity;

        /* Bounce off the top and bottom of the screen */
        if (@y >= Display.height() - 3) {
            @y_velocity = -0.25;
        }
        if (@y <= 0) {
            @y_velocity = 0.25;
        }

        if (@x < -3) {
            destroy this;
        }
    }

    draw {
        return sprite {
            ###
            .#.
            #.#
        };
    }
}
