entity CannonEnemy {
    use Positional;

    var @x_velocity, @fire_timer;

    constructor {
        @x = Display.width() + 1;
        @y = 10;

        @x_velocity = -0.05;

        @fire_timer = spawn Timer;
        @fire_timer.start(120); /* instead of configure, so it doesn't fire immediately on spawn */
    }

    tick {
        this.tick_movement();
        this.tick_hit();
        this.tick_fire();
    }

    func tick_movement() {
        @x = @x + @x_velocity;

        if (@x < -3) {
            destroy this;
        }
    }

    func tick_hit() {
        each bullet in (ShipBullet.all()) {
            if (bullet.has_hit(@x, @y, 3, 3)) {
                destroy this;
                destroy bullet;

                (sound { 0.1: A }).play();
            }
        }
    }

    func tick_fire() {
        if (@fire_timer.elapsed()) {
            bullet = spawn EnemyBullet;
            bullet.move_to(@x, @y + 1);

            @fire_timer.restart();
        }
    }

    draw {
        return sprite {
            .##
            ###
            .##
        };
    }
}
