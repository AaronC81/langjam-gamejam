entity Ship {
    use Positional;

    var @move_timer, @fire_timer;

    constructor {
        @x = 2;
        @y = 2;

        @move_timer = spawn Timer;
        @move_timer.configure(3);

        @fire_timer = spawn Timer;
        @fire_timer.configure(5);
    }

    tick {
        this.tick_movement();
        this.tick_fire();
        this.tick_hit();
    }

    func tick_movement() {
        if (@move_timer.elapsed()) {
            if (Input.down_pressed()) {
                @y = @y + 1;
                @move_timer.restart();
            }

            if (Input.up_pressed()) {
                @y = @y - 1;
                @move_timer.restart();
            }
        }
    }

    func tick_fire() {
        if (Input.z_pressed()) {
            if (@fire_timer.elapsed()) {
                bullet = spawn ShipBullet;
                bullet.move_to(@x + 4, @y + 2);

                @fire_timer.restart();
                
                (sound { 0.025: F }).play();
            }
        }
    }

    func tick_hit() {
        /* TODO: Track health, better audio/sound */
        each bullet in (EnemyBullet.all()) {
            if (bullet.has_hit(@x, @y, 4, 5)) {
                destroy bullet;

                (sound { 0.3: G }).play();
            }
        }
    }

    draw {
        return sprite {
            ##..
            .##.
            .###
            .##.
            ##..
        };
    }
}
