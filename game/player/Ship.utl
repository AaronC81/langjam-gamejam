entity Ship {
    use Positional;

    var @move_cooldown, @fire_cooldown;

    constructor {
        @x = 2;
        @y = 2;
        @move_cooldown = 0;

        @fire_cooldown = 0;
    }

    tick {
        this.tick_movement();
        this.tick_fire();
        this.tick_hit();
    }

    func tick_movement() {
        if (@move_cooldown > 0) {
            @move_cooldown = @move_cooldown - 1;
        }

        if (Input.down_pressed()) {
            if (@move_cooldown == 0) {
                @y = @y + 1;
                @move_cooldown = 3;
            }
        }

        if (Input.up_pressed()) {
            if (@move_cooldown == 0) {
                @y = @y - 1;
                @move_cooldown = 3;
            }
        }
    }

    func tick_fire() {
        if (@fire_cooldown > 0) {
            @fire_cooldown = @fire_cooldown - 1;
        }

        if (Input.z_pressed()) {
            if (@fire_cooldown == 0) {
                bullet = spawn ShipBullet;
                bullet.move_to(@x + 4, @y + 2);

                @fire_cooldown = 5;
                
                (sound { 0.025: F }).play();
            }
        }
    }

    func tick_hit() {
        /* TODO: Track health, better audio/sound */
        each bullet in (EnemyBullet.all()) {
            if (bullet.has_hit(@x, @y, 4, 5)) {
                destroy bullet;

                (sound { 0.3: G }).play();
            }
        }
    }

    draw {
        return sprite {
            ##..
            .##.
            .###
            .##.
            ##..
        };
    }
}
