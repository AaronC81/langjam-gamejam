entity Ship {
    use Positional;

    var @move_timer, @fire_timer;

    constructor {
        @x = 2;
        @y = Display.height() / 2 - 4;

        @move_timer = spawn Timer;
        @move_timer.configure(1);

        @fire_timer = spawn Timer;
        @fire_timer.start(3); /* Don't fire off the "Z" press to start the game */
    }

    tick {
        this.tick_movement();
        this.tick_fire();
        this.tick_hit();
    }

    func tick_movement() {
        if (@move_timer.elapsed()) {
            if (Input.down_pressed()) {
                if (@y < Display.height() - 5) {
                    @y = @y + 1;
                    @move_timer.restart();
                }
            }

            if (Input.up_pressed()) {
                if (@y > 0) {
                    @y = @y - 1;
                    @move_timer.restart();
                }
            }
        }
    }

    func tick_fire() {
        if (Input.z_pressed()) {
            if (@fire_timer.elapsed()) {
                bullet = spawn ShipBullet;
                bullet.move_to(@x + 4, @y + 2);

                @fire_timer.restart();
                
                (sound { 0.025: F }).play();
            }
        }
    }

    func tick_hit() {
        each bullet in (EnemyBullet.all()) { this.die_if_collision(bullet); }
        each enemy in (WobblerEnemy.all()) { this.die_if_collision(enemy); }
        each enemy in (CannonEnemy.all()) { this.die_if_collision(enemy); }
        each enemy in (SprayerEnemy.all()) { this.die_if_collision(enemy); }
    }

    func die_if_collision(obj) {
        if (obj.has_hit(@x, @y, 4, 5)) {
            destroy obj;
            destroy this;

            (spawn Explosion).move_to(obj.x() - 2, obj.y() - 2);
        }
    }

    draw {
        return sprite {
            ##..
            .##.
            .###
            .##.
            ##..
        };
    }
}
